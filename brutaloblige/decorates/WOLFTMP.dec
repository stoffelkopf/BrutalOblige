
//Generalized Base Templates for Enemies
// These are the things that each enemy/boss all have in common


ACTOR WolfEnemy
{
	var int user_active;
	var int user_mapmarker;
	var int user_lost;

	Height 54
	Radius 26
	Mass 10000
	DeathHeight 0 //Must have this and NOT A_NoBlocking/A_Fall so that bodies block doors!
	Painchance 256
	MONSTER 
	+FLOORCLIP
	+FULLVOLACTIVE
	+FULLVOLDEATH
	+DONTGIB
	+FIXMAPTHINGPOS
	BloodColor "FF 00 00"
	DamageFactor "WolfNazi", 0.0
	States
	{
		Raise:
		See:
			"####" A 1 A_JumpIf (user_active > 0, "See.Active")
			"####" A 0 NoiseAlert(0,0) //!!!!!!!!!!!
			"####" A 0 A_GiveInventory("Active", 1) // Keep until Patrol Point code can be updated
			"####" A 0 A_SetUserVar("user_active", 1)
		See.Active:
			"####" A 1 A_JumpIf (user_mapmarker > 0, "See.Marked")
			"####" A 0 A_SetUserVar("user_mapmarker", 1)
		See.Marked:
			"####" A 0 A_Jump(256, "Chase")
		Freeze:
			"####" A 0 A_JumpIf(health <= 0, "Dead")
			"####" A -1
			Stop
		UnFreeze:
			"####" A 0 A_JumpIf(health > 0, "See")
			"####" A 0 A_Jump(256, "Dead")
		Invisible:
			"####" A 0 A_CheckFlag("BOSS","Dead")
			TNT1 A -1
			Stop
	}
}

ACTOR WolfBoss : WolfEnemy
{
	Height 110
	Painchance 0
	+BOSS
	+JUSTHIT
	+AMBUSH
	+LOOKALLAROUND
	+NORADIUSDMG
	+NOINFIGHTING	
	States
	{
		Raise:
			"####" A 0 A_Jump(256, "Dead")
	}
}

ACTOR WolfSoldierTemplate : WolfEnemy
{
	Health 50 //25
	Speed 3
	States
	{
	Spawn:
		"####" A 0
	Spawn.Stand:
		"####" EEEEEE 4 A_LookEx (0, 0, 0, 2048, 0, "See")
		Loop
	Spawn.PatrolNoClip:
		"####" A 0 A_JumpIf(angle % 90 == 0, "TurnAround")
		"####" A 6 A_Warp(AAPTR_DEFAULT, 45, 0, 0, 0, WARPF_STOP | WARPF_INTERPOLATE, "Spawn.Patrol")
		"####" A 6 A_Warp(AAPTR_DEFAULT, 90, 0, 0, 0, WARPF_STOP | WARPF_INTERPOLATE, "Spawn.Patrol")
	TurnAround:
		"####" E 10
		"####" EEE 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" A 0 A_JumpIf((velx != 0) || (vely != 0), "Spawn.Patrol")
		"####" E 10
		"####" EEE 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" A 0 A_JumpIf((velx != 0) || (vely != 0), "Spawn.Patrol")
		"####" A 0 A_SetAngle(angle + 180)
	Spawn.Patrol:
		"####" AAA 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" AAA 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" BBBBBB 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" CCC 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" CCC 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" DDDDDD 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" A 0 A_JumpIf((velx == 0) && (vely == 0), "Spawn.PatrolNoClip")
		Loop
	Chase:
		"####" AAAAA 1 A_Chase
		"####" A 1
		"####" BBBB 1 A_Chase
		"####" CCCCC 1 A_Chase
		"####" CC 1
		"####" DDDD 1 A_Chase
		Goto See
	Missile:
		"####" # 0 A_Stop
		"####" GH 10 A_FaceTarget
		"####" I 8 BRIGHT A_WolfAttack(0, "wolfguard/attack", 1.0, 96, 64, 2, 4, 160.0)
		Goto Chase
	Pain:
		"####" A 0 A_JumpIf(health % 1, "Pain.Alt")
		"####" F 5 A_Pain
		Goto Chase
	Pain.Alt:
		"####" J 5 A_Pain
		Goto Chase
	Pain:
		"####" F 6 A_Pain
		Goto Chase
	Death:
		"####" A 0 A_Scream
		"####" K 7
		"####" L 8
	Dead:
		"####" N -1
		Stop
	}
}

ACTOR WolfSSTemplate : WolfEnemy
{
	Health 100
	Speed 4
	States
	{
	Spawn:
		"####" A 0
	Spawn.Stand:
		"####" EEEEEE 4 A_LookEx (0, 0, 0, 2048, 0, "See")
		Loop
	Spawn.PatrolNoClip:
		"####" A 0 A_JumpIf(angle % 90 == 0, "TurnAround")
		"####" A 6 A_Warp(AAPTR_DEFAULT, 45, 0, 0, 0, WARPF_STOP | WARPF_INTERPOLATE, "Spawn.Patrol")
		"####" A 6 A_Warp(AAPTR_DEFAULT, 90, 0, 0, 0, WARPF_STOP | WARPF_INTERPOLATE, "Spawn.Patrol")
	TurnAround:
		"####" E 10
		"####" EEE 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" A 0 A_JumpIf((velx != 0) || (vely != 0), "Spawn.Patrol")
		"####" E 10
		"####" EEE 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" A 0 A_JumpIf((velx != 0) || (vely != 0), "Spawn.Patrol")
		"####" A 0 A_SetAngle(angle + 180)
	Spawn.Patrol:
		"####" AAA 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" AAA 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" BBBBBB 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" CCC 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" CCC 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" DDDDDD 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" A 0 A_JumpIf((velx == 0) && (vely == 0), "Spawn.PatrolNoClip")
		Loop
	Chase:
		"####" AAAAA 1 A_Chase
		"####" A 1
		"####" BBBB 1 A_Chase
		"####" CCCCC 1 A_Chase
		"####" CC 1
		"####" DDDD 1 A_Chase
		Loop
	Missile:
		"####" # 0 A_Stop
		"####" GH 10 A_FaceTarget
		"####" I 5 BRIGHT A_WolfAttack(0, "bluess/attack", 0.666, 64, 64, 2, 4, 160.0)
		"####" H 5 A_FaceTarget
		"####" I 5 BRIGHT A_WolfAttack(0, "bluess/attack", 0.666, 64, 64, 2, 4, 160.0)
		"####" H 5 A_FaceTarget
		"####" I 5 BRIGHT A_WolfAttack(0, "bluess/attack", 0.666, 64, 64, 2, 4, 160.0)
		"####" H 5 A_FaceTarget
		"####" I 5 BRIGHT A_WolfAttack(0, "bluess/attack", 0.666, 64, 64, 2, 4, 160.0)
		Goto Chase
	Pain:
		"####" A 0 A_JumpIf(health % 1, "Pain.Alt")
		"####" F 5 A_Pain
		Goto Chase
	Pain.Alt:
		"####" J 5 A_Pain
		Goto Chase
	Death:
		"####" A 0 A_SpawnItemEx("Mp40",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,128)
		"####" A 0 A_Scream
		"####" K 7
		"####" L 8
	Dead:
		"####" N -1 
		Stop
	}
}

ACTOR WolfDogTemplate : WolfEnemy
{
	Health 50
	Speed 5
	+NOINFIGHTING
	-CANUSEWALLS
	-ACTIVATEMCROSS
	MeleeDamage 2
	States
	{
	Spawn:
		"####" A 0
		Goto Spawn.Patrol
	Spawn.PatrolNoClip:
		"####" A 0 A_JumpIf(angle % 90 == 0, "TurnAround")
		"####" A 6 A_Warp(AAPTR_DEFAULT, 45, 0, 0, 0, WARPF_STOP | WARPF_INTERPOLATE, "Spawn.Patrol")
		"####" A 6 A_Warp(AAPTR_DEFAULT, 90, 0, 0, 0, WARPF_STOP | WARPF_INTERPOLATE, "Spawn.Patrol")
	TurnAround:
		"####" AB 5
		"####" CCC 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" A 0 A_JumpIf((velx != 0) || (vely != 0), "Spawn.Patrol")
		"####" DA 5
		"####" BBB 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" A 0 A_JumpIf((velx != 0) || (vely != 0), "Spawn.Patrol")
		"####" A 0 A_SetAngle(angle + 180)
	Spawn.Patrol:
		"####" AAA 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" AAA 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" BBBBBB 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" CCC 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" CCC 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" DDDDDD 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" A 0 A_JumpIf((velx == 0) && (vely == 0), "Spawn.PatrolNoClip")
		Loop
	Spawn.Stand:
		"####" AAAABBBBCCCCDDDD 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		Loop
	Wander:
		"####" AA 1 A_Wander
		"####" AB 1 A_Look
		"####" BB 1 A_Wander
		"####" BB 1 A_Look
		"####" CC 1 A_Wander
		"####" CC 1 A_Look
		"####" DD 1 A_Wander
		"####" DD 1 A_Look
		Loop
	Chase:
		"####" AAAAA 1 A_Chase
		"####" A 1
		"####" BBBB 1 A_Chase
		"####" CCCCC 1 A_Chase
		"####" CC 1
		"####" DDDD 1 A_Chase
		Goto See
	Melee:
		"####" # 0 A_Stop
		"####" EF 5 A_FaceTarget
		"####" G 5 A_CustomMeleeAttack(random(1,15))
		"####" EA 5
		Goto Chase
	Death:
		"####" A 0 A_Scream
		"####" HI 5
	Dead:
		"####" K -1
		Stop
	}
}

ACTOR WolfOfficerTemplate : WolfEnemy
{
	Speed 5
	Health 100
	States
	{
	Spawn:
		"####" A 0
	Spawn.Stand:
		"####" ZZZZZZ 2 A_LookEx (0, 0, 0, 2048, 0, "See")
		Loop
	Spawn.PatrolNoClip:
		"####" A 0 A_JumpIf(angle % 90 == 0, "TurnAround")
		"####" A 6 A_Warp(AAPTR_DEFAULT, 45, 0, 0, 0, WARPF_STOP | WARPF_INTERPOLATE, "Spawn.Patrol")
		"####" A 6 A_Warp(AAPTR_DEFAULT, 90, 0, 0, 0, WARPF_STOP | WARPF_INTERPOLATE, "Spawn.Patrol")
	TurnAround:
		"####" Z 10
		"####" ZZ 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" A 0 A_JumpIf((velx != 0) || (vely != 0), "Spawn.Patrol")
		"####" Z 10
		"####" ZZ 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" A 0 A_JumpIf((velx != 0) || (vely != 0), "Spawn.Patrol")
		"####" A 0 A_SetAngle(angle + 180)
	Spawn.Patrol:
		"####" AA 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" AA 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" BBBB 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" CC 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" CC 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" DDDD 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" A 0 A_JumpIf((velx == 0) && (vely == 0), "Spawn.PatrolNoClip")
		Loop
	Chase:
		"####" AAAAA 1 A_Chase
		"####" A 1
		"####" BBBB 1 A_Chase
		"####" CCCCC 1 A_Chase
		"####" CC 1
		"####" DDDD 1 A_Chase
		Loop
	Missile:
		"####" # 0 A_Stop
		"####" E 3 A_FaceTarget
		"####" F 10 A_FaceTarget
		"####" G 5 BRIGHT A_WolfAttack(0, "officer/attack", 1.0, 64, 64, 2, 4, 160.0)
		Goto Chase
	Pain:
		"####" A 0 A_JumpIf(health % 1, "Pain.Alt")
		"####" H 5 A_Pain
		Goto Chase
	Pain.Alt:
		"####" L 5 A_Pain
		Goto Chase
	Death:
		"####" A 0 A_Scream
		"####" I 5
		"####" J 6
		"####" M 6
	Dead:
		"####" N -1 
		Stop
	}
}

ACTOR WolfBossGrosseTemplate : WolfBoss 
//Base template for Hans, Gretel, and Trans
{
	Health 850
	Speed 3
	MaxTargetRange 256
	States
	{
	Spawn:
		"####" A 0
	Spawn.Stand:
		"####" A 5 A_Look
		Goto Spawn + 1
	Walk:
		"####" AAAAA 1 A_Chase ("", "")
		"####" A 1
		"####" BBBB 1 A_Chase ("", "")
		"####" CCCCC 1 A_Chase ("", "")
		"####" CC 1
		"####" DDDD 1 A_Chase ("", "")
	Chase:
		"####" AAAAA 1 A_Chase ("", "Attack")
		"####" A 1
		"####" BBBB 1 A_Chase ("", "Attack")
		"####" CCCCC 1 A_Chase ("", "Attack")
		"####" CC 1
		"####" DDDD 1 A_Chase ("", "Attack")
		Loop
	Attack:
		"####" E 15 A_FaceTarget
		"####" F 5 A_FaceTarget
		"####" GFGFGE 5 BRIGHT A_WolfAttack(0, "gretel/attack", 0.666, 64, 64, 2, 4, 160.0)
		"####" A 0 A_JumpIfCloser(64, "Attack")
		Goto Walk
	Death:
		"####" I 8 A_Scream
		"####" J 7 A_BossDeath
	Dead:
		"####" K -1 
		Stop
	}
}

ACTOR WolfBossSchabbsTemplate : WolfBoss
{
	Health 850
	Speed 4
	States
	{
	Spawn:
		"####" A 0
	Spawn.Stand:
		"####" A 5 A_Look
		Loop
	Chase:
		"####" AAAAA 1 A_Chase
		"####" A 1
		"####" BBBB 1 A_Chase
		"####" CCCCC 1 A_Chase
		"####" CC 1
		"####" DDDD 1 A_Chase
		Loop
	Missile:
		"####" E 15 A_FaceTarget
		"####" F 5 A_CustomMissile ("Syringe", 0, 18, 0, CMF_OFFSETPITCH, -2)
		Goto Chase
	Death:
		"####" A 35 A_Scream //75
		"####" H 5
		"####" J 5
		"####" K 5 
		"####" K -1 A_BossDeath
	Dead:
		"####" K -1
		Stop
	}
}

ACTOR WolfBossHitlerMechTemplate : WolfBoss
{
	Health 400
	Speed 2
	States
	{
	Spawn:
		"####" A 0
	Spawn.Stand:
		"####" AAAAAAAAAAAA 1 A_Look
		Loop
	Chase:
	    "####" D 0 A_PlaySound("hitler/active")
		"####" AAAAA 1 A_Chase
		"####" AAA 1 A_Pain
		"####" BBBB 1 A_Chase
		"####" CCCCC 1 A_Chase
		"####" CCC 1 A_Pain
		"####" DDDD 1 A_Chase		
		Loop
	Missile:
		"####" E 15 A_FaceTarget
		"####" F 5 A_FaceTarget
		"####" GFGF 5 BRIGHT A_WolfAttack(0, "hitler/attack", 1.0, 64, 64, 2, 4, 160.0)
		Goto Chase
	Death:
		"####" H 5 A_Scream
		"####" I 5
		"####" K 1 A_BossDeath
	Dead:
		"####" K -1
		Stop
	}
}

ACTOR WolfBossHitlerTemplate : WolfBoss
{
	Health 400
	Speed 4
	States
	{
	Spawn:
		"####" A 0
	Spawn.Stand:
		"####" A 5 A_Look
		Loop
	Chase:
		"####" AAA 1 A_Chase
		"####" AA 1
		"####" B 1 A_Chase
		"####" CCC 1 A_Chase
		"####" CC 1
		"####" D 1 A_Chase
		Loop
	Missile:
		"####" G 15 A_FaceTarget
		"####" H 5 A_FaceTarget
		"####" IHIH 5 BRIGHT A_WolfAttack(0, "hitler/attack", 1.0, 64, 64, 2, 4, 160.0)
		Goto Chase
	Death:
		"####" A 70 A_Scream
		"####" J 5 A_Pain
		"####" LMNO 5
		"####" P 5 A_BossDeath
	Dead:
		"####" Q -1
		Stop
	}
}

ACTOR WolfBossFatFaceTemplate : WolfBoss
{
	Health 850
	Speed 4
	States
	{
	Spawn:
		"####" A 0
	Spawn.Stand:
		"####" A 5 A_Look
		Loop
	Chase:
		"####" AAAAA 1 A_Chase
		"####" A 1
		"####" BBBB 1 A_Chase
		"####" CCCCC 1 A_Chase
		"####" CC 1
		"####" DDDD 1 A_Chase
		Loop
	Missile:
		"####" E 15 A_FaceTarget
		"####" F 5 A_FaceTarget
		"####" G 5 BRIGHT A_CustomMissile ("WolfRocket", 30, 30, 0, CMF_OFFSETPITCH, -2)
		"####" E 0 A_FaceTarget
		"####" HGH 5 BRIGHT A_WolfAttack(0, "fat/attack", 1.0, 64, 64, 2, 4, 160.0)
		Goto Chase
	Death:
		"####" A 20 A_Scream
		"####" JK 5
		"####" L 4 A_BossDeath
	Dead:
		"####" M -1
		Stop
	}
}

ACTOR WolfBossGiftmacherTemplate : WolfBoss
{
	Health 850
	Speed 3
	States
	{
	Spawn:
		"####" A 0
	Spawn.Stand:
		"####" A 5 A_Look
		Loop
	Chase:
		"####" AAAAA 1 A_Chase
		"####" A 1
		"####" BBBB 1 A_Chase
		"####" CCCCC 1 A_Chase
		"####" CC 1
		"####" DDDD 1 A_Chase
		Loop
	Missile:
		"####" E 15 A_FaceTarget
		"####" F 5 BRIGHT A_CustomMissile ("WolfRocket", 30, 13, 0, CMF_OFFSETPITCH, -2)
		Goto Chase
	Death:
		"####" A 20 A_Scream
		"####" GH 5
		"####" J 1 A_BossDeath
	Dead:
		"####" J -1
		Stop
	}
}

Actor WolfBossUberMutantTemplate : WolfBoss
{
	Health 1050
	Speed 5
	States
	{
	Spawn:
		"####" A 0
	Spawn.Stand:
		"####" A 5 A_Look
		Loop
	Chase:
		"####" AAAAA 1 A_Chase
		"####" A 1
		"####" BBBB 1 A_Chase
		"####" CCCCC 1 A_Chase
		"####" CC 1
		"####" DDDD 1 A_Chase
		Loop
	Missile:
		"####" E 15 A_FaceTarget
		"####" F 6 BRIGHT A_WolfAttack(0, "uber/attack", 1.0, 64, 64, 2, 4, 160.0)
		"####" E 0 A_FaceTarget
		"####" G 6 BRIGHT A_WolfAttack(0, "uber/attack", 1.0, 64, 64, 2, 4, 160.0)
		"####" E 0 A_FaceTarget
		"####" H 6 BRIGHT A_WolfAttack(0, "uber/attack", 1.0, 64, 64, 2, 4, 160.0)
		"####" E 0 A_FaceTarget
		"####" G 6 BRIGHT A_WolfAttack(0, "uber/attack", 1.0, 64, 64, 2, 4, 160.0)
		"####" E 0 A_FaceTarget
		"####" F 6 BRIGHT A_WolfAttack(0, "uber/attack", 1.0, 64, 64, 2, 4, 160.0)
		"####" E 0 A_FaceTarget
		Goto Chase
	Death:
		"####" A 25 A_Scream
		"####" J 8
		"####" K 7
		"####" L 8 A_BossDeath
	Dead:
		"####" M -1
		Stop
	}
}

ACTOR WolfBossDeathKnightTemplate : WolfBoss
{
	Health 1250
	Speed 4
	States
	{
	Spawn:
		"####" A 0
	Spawn.Stand:
		"####" A 5 A_Look
		Loop
	Chase:
	    "####" D 0 A_PlaySound("dknight/active")		
		"####" AAAAA 1 A_Chase
		"####" A 1
		"####" BBBB 1 A_Chase
		"####" CCCCC 1 A_Chase
		"####" CC 1
		"####" DDDD 1 A_Chase
		Loop
	Missile:
		"####" F 15 A_FaceTarget
		"####" G 5 BRIGHT A_CustomMissile ("WolfRocketSoD", 64, 30, 0, CMF_OFFSETPITCH, -2)
		"####" I 5 BRIGHT A_WolfAttack(0, "dknight/attack", 1.0, 64, 64, 2, 4, 160.0)
		"####" I 0 A_FaceTarget
		"####" H 5 BRIGHT A_CustomMissile ("WolfRocketSoD", 64, -30, 0, CMF_OFFSETPITCH, -2)
		"####" I 5 BRIGHT A_WolfAttack(0, "dknight/attack", 1.0, 64, 64, 2, 4, 160.0)
		Goto Chase
	Death:
		"####" A 20 A_Scream
		"####" MNO 5
		"####" P 5 A_BossDeath
	Dead:
		"####" Q -1
		Stop
	}
}

Actor WolfMutantTemplate : WolfEnemy
{
	Health 45
	Speed 3
	States
	{
	Spawn:
		"####" A 0
	Spawn.Stand:
		"####" ZZZZZZ 3 A_LookEx (0, 0, 0, 2048, 0, "See")
		Loop
	Spawn.PatrolNoClip:
		"####" A 0 A_JumpIf(angle % 90 == 0, "TurnAround")
		"####" A 6 A_Warp(AAPTR_DEFAULT, 45, 0, 0, 0, WARPF_STOP | WARPF_INTERPOLATE, "Spawn.Patrol")
		"####" A 6 A_Warp(AAPTR_DEFAULT, 90, 0, 0, 0, WARPF_STOP | WARPF_INTERPOLATE, "Spawn.Patrol")
	TurnAround:
		"####" Z 10
		"####" ZZZ 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" A 0 A_JumpIf((velx != 0) || (vely != 0), "Spawn.Patrol")
		"####" Z 10
		"####" ZZZ 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" A 0 A_JumpIf((velx != 0) || (vely != 0), "Spawn.Patrol")
		"####" A 0 A_SetAngle(angle + 180)
	Spawn.Patrol:
		"####" AAA 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" AAA 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" BBBBBB 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" CCC 1 ThrustThing (angle * 256 / 360, 1, 0, 0)
		"####" CCC 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" DDDDDD 1 A_LookEx (0, 0, 0, 2048, 0, "See")
		"####" A 0 A_JumpIf((velx == 0) && (vely == 0), "Spawn.PatrolNoClip")
		Loop
	Chase:
		"####" AAAAA 1 A_Chase
		"####" A 1
		"####" BBBB 1 A_Chase
		"####" CCCCC 1 A_Chase
		"####" CC 1
		"####" DDDD 1 A_Chase
		Loop
	Missile:
		"####" # 0 A_Stop
		"####" G 3 A_FaceTarget
		"####" H 10 BRIGHT A_WolfAttack(0, "mutant/attack", 1.0, 64, 64, 2, 4, 160.0)
		"####" G 5 A_FaceTarget
		"####" I 10 BRIGHT A_WolfAttack(0, "mutant/attack", 1.0, 64, 64, 2, 4, 160.0)
		"####" G 0 A_JumpIfCloser(64.0, "Missile")
		Goto Chase
	Pain:
		"####" A 0 A_JumpIf(health % 1, "Pain.Alt")
		"####" E 5 A_Pain
		Goto Chase
	Pain.Alt:
		"####" J 5 A_Pain
		Goto Chase
	Death:
		"####" A 0 A_Scream
		"####" K 3
		"####" L 4
		"####" M 3
	Dead:
		"####" O -1 
		Stop
	}
}

ACTOR WolfHitlerGhostTemplate : WolfEnemy
{
	Health 200
	Speed 4
	Painchance 0
	+DROPOFF
	+FLOAT
	+NORADIUSDMG
	+AMBUSH
	+LOOKALLAROUND
	+NOINFIGHTING
	States
	{
	Spawn:
		"####" A 0
	Spawn.Stand:
		"####" A 5 A_Look
		Loop
	Chase:
		"####" AAAAA 1 A_Chase
		"####" A 1
		"####" BBBB 1 A_Chase
		"####" CCCCC 1 A_Chase
		"####" CC 1
		"####" DDDD 1 A_Chase
		Loop
	Missile:
		"####" E 4 A_FaceTarget
		"####" EEEEEEEE 4 BRIGHT A_CustomMissile ("GhostFireBall", 20, 0, 0, CMF_OFFSETPITCH, -3)
		Goto Chase
	Death:
		"####" F 5
		"####" G 5 A_Scream
		"####" HI 5
	Dead:
		"####" K -1
		Stop
	}
}
